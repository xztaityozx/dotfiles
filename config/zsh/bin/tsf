#!/usr/bin/env zsh

function usage() {
  cat << USAGE
tmux switcher with fzf

Usage:

  $ tsf [-wsp] [-h|--help] [-v|--version]

Options:
  -w                 switch window
  -s                 switch session
  -p                 switch pane
  --clear            clear fifo for this script
  --include-attached include attached session/window/pane to list
  -v, --version      print version and exit
  -h, --help         print this help and exit

USAGE

  exit 1;
}

function version() {
  echo "tsf v1.0.0"
  exit 0;
}

function defer() {
  local filename="$fifo"
  [[ ! -z "$filename" ]] && [[ -e "$filename" ]] && rm $filename
}

function clear() {
  rm $HOME/.tmux/np-*
  exit $?
}

function main() {
  type tmux &> /dev/null || {  echo -e "\e[31m[ERR | $(date)] tmux required\e[39m" && exit 1; }

  zmodload zsh/zutil
  local -A opthash
  zparseopts -D -A opthash -- w s p h v -clear -help -version -include-attached || exit 1

  if [[ -n "${opthash[(i)--help]}" ]] || [[ -n "${opthash[(i)-h]}" ]]; then
    usage
  fi

  if [[ -n "${opthash[(i)--version]}" ]] || [[ -n "${opthash[(i)-v]}" ]]; then
    version
  fi

  if [[ -n "${opthash[(i)--clear]}" ]] then
    clear
  fi

  fifo="${HOME}/.tmux/np-${RANDOM}"

  local mode=window
  [[ -n "${opthash[(i)-w]}" ]] && mode=window
  [[ -n "${opthash[(i)-p]}" ]] && mode=pane
  [[ -n "${opthash[(i)-s]}" ]] || [[ -z "$TMUX" ]] && mode=session

  local filter="tmux list-${mode}s | rg '\(a.+\)\$' | fzf | sel -d: 1"
  [[ ! -n "${opthash[(i)--include-attached]}" ]] && filter="tmux list-${mode}s | fzf | sel -d: 1"

  local target="";
  
  if [[ -z "$TMUX" ]] then
    tmux list-sessions &> /dev/null || {tmux; return}
    target=$(eval $filter)
  else
    
    local fifo=$HOME/.tmux/fifo-$RANDOM
    trap defer

    tmux splitw "$filter > $fifo" && target=$(cat $fifo)
    defer
  fi


  if [[ "$mode" == "window" ]] || [[ "$mode" == "pane" ]] then
    tmux select-${mode} -t $target
  elif [[ -z "$TMUX" ]] then
    tmux attach -t $target
  else
    tmux switch -t $target
  fi
}

main "$@"
